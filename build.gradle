plugins {
    id 'fabric-loom' version '0.9-SNAPSHOT'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version "1.1.2"
    id "com.modrinth.minotaur" version "1.1.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

def env = System.getenv()
version = version + "+" + (env.GITHUB_RUN_NUMBER ? ("build." + env.GITHUB_RUN_NUMBER) : "custom")

repositories {
    maven { url "https://maven.shedaniel.me" }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:6.0.298-alpha"
    modRuntime "me.shedaniel:RoughlyEnoughItems-fabric:6.0.298-alpha"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

processResources {
    inputs.property "version", project.version
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
        if (env.REPO_PASSWORD) {
            maven {
                url = "https://repo.hexeption.dev/repository/hex-mods/"
                credentials {
                    username = "admin"
                    password = env.REPO_PASSWORD
                }
            }
        }
    }
}

curseforge {
    if (env.CURSE_API_KEY) {
        apiKey = env.CURSE_API_KEY
    }
    project {
        id = '439897'
        changelog = 'A changelog can be found at https://github.com/Hexeption/ColouredBundles'
        releaseType = 'beta'
        addGameVersion '1.17'
        mainArtifact(remapJar) {
            displayName = "ColouredBundles v$version for $project.minecraft_version"
        }
        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload){
    if (env.MODRINTH_API_KEY) {
        token = env.MODRINTH_API_KEY
    }
    projectId = '70Jd8vSb'
    versionNumber = "$version"
    uploadFile = remapJar
    addGameVersion("$minecraft_version")
    addLoader('fabric')
}
